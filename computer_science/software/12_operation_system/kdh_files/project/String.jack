// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array str;
    field int length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let str = Array.new(1);
        } else {
            let str = Array.new(maxLength);
        }
        let length = 0;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do str.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return str[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let str[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        let str[length] = c;
        let length = length + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length > 0) {
            let str[length-1] = 0;
            let length = length - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int i; // 인덱스
        var int val; // 전체 정수 값
        var int d; // 각 자리수 값
        let val = 0;
        let i = 0;
        while (i < length) {
            if ((str[i] > 47) & (str[i] < 58)) {
                let d = str[i] - 48;
                let val = (val * 10) + d;
            }
            let i = i + 1;
        }

        if (str[0] = 45) { // 45 represents '-'
            let val = -val;
        }
        return val;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        var int lastDigit, c;
        let lastDigit = Math.abs(val) - Math.abs((10 * (val / 10)));  // least significant decimal
        let c = 48 + lastDigit;

        if (Math.abs(val) < 10) {
            while (~(length = 0)) {
                do eraseLastChar();
            }
            if (val < 0) {
                do appendChar(45); // '-' 문자 추가
            }
            do appendChar(c); // 마지막 자리수 문자 추가
        } else { // 두 자리 이상인 경우
            do setInt(val / 10);
            do appendChar(c); // 마지막 자리수 문자 추가
        }
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
